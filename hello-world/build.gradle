plugins {
	id 'org.springframework.boot' version '2.1.7.RELEASE'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'io.pivotal.pcc.helloworld'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	maven { url = "https://repo.spring.io/libs-milestone" }
}

dependencies {
	implementation('org.springframework.geode:spring-gemfire-starter:1.1.0.RELEASE')
	implementation ('org.springframework.boot:spring-boot-starter-web')
}

task createClusterDir(type:Exec) {
	commandLine 'mkdir', 'cluster'
}

task startServer(type: Exec) {
	dependsOn 'createClusterDir'
	workingDir './cluster'
	commandLine 'gfsh', 'run', '--file=../scripts/start.gfsh'
}

task shutdownServer(type: Exec) {
	commandLine 'gfsh', 'run', '--file=./scripts/stop.gfsh'
}

task cleanUpClusterDirectory(type: Exec) {
	commandLine 'rm', '-r', 'cluster'
}

task stopServer {
	dependsOn 'shutdownServer'
	dependsOn 'cleanUpClusterDirectory'
}

bootRun {
    jvmArgs = ["-Dspring.profiles.active=localCluster"]
}