/*
 * Cloud Cache Examples
 *
 * Copyright (c) 2019-Present Pivotal Software, Inc. All Rights Reserved. 
 *
 * This product is licensed to you under the Apache License, Version 2.0 (the "License"). You may not use this product except in compliance with the License. 
 *
 * This product may include a number of subcomponents with separate copyright notices and license terms. Your use of these subcomponents is subject to the terms and conditions of the subcomponent's license, as noted in the LICENSE file.
 */

buildscript{
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'java'
}

apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'

group = 'io.pivotal.cloudcache.helloworld'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        credentials {
            username "$gemfireReleaseRepoUser"
            password "$gemfireReleaseRepoPassword"
        }
        url "https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire"
    }
}

dependencies {
    implementation("org.springframework.geode:spring-gemfire-starter:${springBootStarterVersion}")
    implementation('org.springframework.boot:spring-boot-starter-web')
}

task createClusterDirectory(type: Exec) {
    group = 'cluster'
    dependsOn 'cleanUpClusterDirectory'
    commandLine 'mkdir', 'cluster'
}

task startCluster(type: Exec) {
    group = 'cluster'
    dependsOn 'createClusterDirectory'
    workingDir './cluster'
    commandLine 'gfsh', 'run', '--file=../scripts/start.gfsh'
}

task stopCluster(type: Exec) {
    group = 'cluster'
    commandLine 'gfsh', 'run', '--file=./scripts/stop.gfsh'
}

task cleanUpClusterDirectory(type: Exec) {
    group = 'cluster'
    commandLine 'rm', '-rf', 'cluster'
}

task stopAndCleanUpCluster {
    group = 'cluster'
    dependsOn 'stopCluster'
    dependsOn 'cleanUpClusterDirectory'
}

bootRun {
    jvmArgs = ["-Dspring.profiles.active=localCluster"]
}